" vim:ft=vim

" FZF Mappings search
nmap <LEADER><TAB> <plug>(fzf-maps-n)
xmap <LEADER><TAB> <plug>(fzf-maps-x)
omap <LEADER><TAB> <plug>(fzf-maps-o)

" Arrow keys to resize in normal mode
map <down> <C-W>+
map <up> <C-W>-
map <right> <C-W>>
map <left> <C-W><

" Enable dot command over visual selection
xmap . :normal .<CR>

" Disable entering Ex mode
nnoremap Q <NOP>

" Highlight and Search for word under the cursor without changing the cursor position
nnoremap <silent> * :PreserveSave<CR>:normal! *N<CR>:set hlsearch<CR>:PreserveRestore<CR>

" use j and k if a count is given, gj and gk otherwise
noremap <silent> <expr> j (v:count == 0 ? 'gj' : 'j')
noremap <silent> <expr> k (v:count == 0 ? 'gk' : 'k')

" Make Y behave similarly to D and C - yank to EOL
nnoremap Y y$

" Move to beginning/end of line
nnoremap H ^
nnoremap L $

" Visually select text entered last time in insert
nnoremap gV `[v`]

" Stay in visual mode when indenting. You will never have to run gv after
" performing an indentation.
vnoremap < <gv
vnoremap > >gv

" %% for current buffer path and filename (relative)
cnoremap %% <C-R>=fnameescape(expand('%'))<CR>
" :: for current buffer directory (relative)
cnoremap :: <C-R>=fnameescape(expand('%:h'))<CR>

" Tab navigation
nnoremap [t :tprevious<CR>
nnoremap ]t :tnext<CR>

" Unimpaired-like toggle for colorcolumn > 80
nnoremap yoy :call ToggleColorcolumn()<CR>

" Keep search matches in the middle of the window.
" (currently mimicked in vim-indexed-search plugin
" nnoremap n nzzzv
" nnoremap N Nzzzv

" Use sensible regexes
nnoremap / /\v

vnoremap * :<C-u>call <SID>VSetSearch()<CR>//<CR><c-o>
vnoremap # :<C-u>call <SID>VSetSearch()<CR>??<CR><c-o>


" removes hash from comment delimiters so my text lists format properly
" (<CR> returns to same indent as -, not indent of first word)
autocmd FileType text setlocal comments=fb:*

" Only have cursorline in active window.
" autocmd WinEnter * setlocal cursorline
" autocmd WinLeave * setlocal nocursorline

" Automatically rebalance panes on window resize
autocmd VimResized * :wincmd =

" Set filetype to text if it is unset
autocmd BufEnter * if &filetype == "" | setlocal ft=text | endif

" jump to last position in file
autocmd BufReadPost *
  \ if line("'\"") > 0 && line("'\"") <= line("$") |
  \   exe "normal g`\"" |
  \ endif

" hitting Esc twice in FZF windows closes them
augroup fzf
  autocmd!
  autocmd FileType fzf nmap <buffer> <ESC> :q<CR>:echo<CR>
augroup END


" --- Abbreviations ---------------------------------------------------------- "
iab cm ✓
iab lmda λ


" --- Functions and Commands ------------------------------------------------- "
function! ToggleColorcolumn()
  if &colorcolumn==0
    execute "set colorcolumn=" . join(range(81,335), ',')
    echo "colorcolumn>80"
  else
    set colorcolumn=0
    echo "colorcolumn=0"
  endif
endfunction


" Convenient command to see the difference between the current buffer and the
" file it was loaded from, thus the changes you made.
" Only define it when not defined already.
if !exists(":DiffOrig")
  command DiffOrig vert new | set bt=nofile | r ++edit # | 0d_ | diffthis
      \ | wincmd p | diffthis
endif


" Send the output of a cmd to a new tab (or split)
" ex: :TabMessage highlight
function! TabMessage(cmd)
  redir => message
  silent execute a:cmd
  redir END
  if empty(message)
    echoerr "no output"
  else
    " use "new" instead of "tabnew" below if you prefer split windows instead of tabs
    vnew
    setlocal buftype=nofile bufhidden=wipe noswapfile nobuflisted nomodified
    silent put=message
  endif
endfunction
command! -nargs=+ -complete=command TabMessage call TabMessage(<q-args>)


function! XTermPasteBegin()
  set pastetoggle=<Esc>[201~
  set paste
  return ""
endfunction
imap <special> <expr> <Esc>[200~ XTermPasteBegin()


" Visual Mode */# from Scrooloose
function! s:VSetSearch()
  let temp = @@
  norm! gvy
  let @/ = '\V' . substitute(escape(@@, '\'), '\n', '\\n', 'g')
  let @@ = temp
endfunction


" ---------------------------------------------------------------------------- "
"  From Gary Bernhardt's .vimrc file
"    RemoveFancyCharacters COMMAND
"    Remove smart quotes, etc.
" ---------------------------------------------------------------------------- "
function! RemoveFancyCharacters()
    let typo = {}
    let typo["“"] = '"'
    let typo["”"] = '"'
    let typo["‘"] = "'"
    let typo["’"] = "'"
    let typo["–"] = '--'
    let typo["—"] = '---'
    let typo["…"] = '...'
    :exe ":%s/".join(keys(typo), '\|').'/\=typo[submatch(0)]/gec'
endfunction
command! RemoveFancyCharacters :call RemoveFancyCharacters()

